import helpers { read_lines }

function panic(anon message: String) -> never {
    eprintln("PANIC: {}", message)
    abort()
}

function parse(anon s: String) -> i8 {
    let ret = match s {
        "A"|"X" => 1 // Rock
        "B"|"Y" => 2 // Paper
        "C"|"Z" => 3 // Scissors
        else => 0
    }
    if ret == 0 {
        panic("invalid value")
    }
    return ret
}

function score(opp: i8, player: i8) -> i64 {
    // rock < paper 
    // paper < scissors
    // scissors < rock
    mut ret = player
    if player == opp {
        ret += 3
    } else if (player == 1 and opp == 3)
            or (player == 2 and opp == 1)
            or (player == 3 and opp == 2) {
        ret += 6
    }
    return ret
}

function solve2(opp: i8, player: i8) -> i8 {
    if player == 1 {
        if opp == 1 {
            return 3
        }
        return opp-1
    } else if player == 2 {
        return opp
    } else if player == 3 {
        return opp%3+1
    }
    panic("invalid value")
}

function main(args: [String]) {
    mut total1 = 0
    mut total2 = 0
    for line in read_lines(filename: "input") {
        let split = line.split(' ')
        let opp = parse(split[0])
        let player1 = parse(split[1])
        total1 += score(opp, player: player1)
        let player2 = solve2(opp, player: player1)
        total2 += score(opp, player: player2)
    }
    println("Part 1: {}", total1)
    println("Part 2: {}", total2)
}
