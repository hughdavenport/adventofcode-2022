import helpers { read_lines }

struct Point {
    x: i64
    y: i64

    function parse(anon s: String) throws -> Point {
        let split = s.split(',')
        return Point(
            x: split[0].to_int()! as! i64,
            y: split[1].to_int()! as! i64
        )
    }
}

struct Box {
    origin: Point
    corner: Point

    function adjust_bounds(mut this, anon p: Point) {
        if p.x < this.origin.x {
            this.origin.x = p.x
        }
        if p.y < this.origin.y {
            this.origin.y = p.y
        }
        if p.x > this.corner.x {
            this.corner.x = p.x
        }
        if p.y > this.corner.y {
            this.corner.y = p.y
        }
    }

    function adjust_bounds(mut this, anon b: Box) {
        this.adjust_bounds(b.origin)
        this.adjust_bounds(b.corner)
    }

    function size(this) -> usize {
        return this.width() * this.height()
    }

    function width(this) -> usize {
        return (this.corner.x - this.origin.x + 1) as! usize
    }

    function height(this) -> usize {
        return (this.corner.y - this.origin.y + 1) as! usize
    }

    function normalise(this, x: i64, y: i64) -> usize {
        let (_x, _y) = (
            (x - this.origin.x) as! usize,
            (y - this.origin.y) as! usize
        )
        return _y * this.width() + _x
    }

    function increase(mut this) {
        this.corner.x ++
        this.corner.y ++
    }

    function increase_for_overflow(mut this) {
        this.origin.x --
        this.corner.x ++
        this.corner.y ++
    }
}

struct Path {
    points: [Point]
    box: Box

    function parse(anon s: String) throws -> Path {
        mut points: [Point] = []
        mut box: Box? = None
        let split = s.split('>')
        for p_s in split {
            let p = Point::parse(p_s.split(' ')[0])
            points.push(p)
            if not box.has_value() {
                box = Box(origin: p, corner: p)
            } else {
                box!.adjust_bounds(p)
            }
        }
        return Path(points, box: box!)
    }
}

enum Thing {
    EMPTY
    PATH
    START
    SAND

    function to_byte(this) -> u8 => match this {
        EMPTY => b'.'
        PATH => b'#'
        START => b'+'
        SAND => b'o'
    }
}

struct SandMap {
    box: Box
    sand: Point
    grid: [Thing]

    function parse(anon lines: [String]) throws -> SandMap {
        mut paths: [Path] = []
        mut box: Box? = None
        for line in lines {
            let path = Path::parse(line)
            paths.push(path)
            if not box.has_value() {
                box = path.box
            } else {
                box!.adjust_bounds(path.box)
            }
        }

        let sand = Point(x: 500, y: 0)
        box!.adjust_bounds(sand)
        box!.increase()
        box!.increase_for_overflow()
        mut grid: [Thing] = [Thing::EMPTY(); box!.size()]
        mut map = SandMap(box: box!, sand: sand, grid)
        grid[map.idx(x: sand.x, y: sand.y)] = Thing::START()
        for p in paths {
            map.draw_path(p)
        }
        return map
    }

    function idx(this, x: i64, y: i64) -> usize {
        return this.box.normalise(x, y)
    }

    function get(this, x: i64, y: i64) -> Thing {
        return this.grid[this.idx(x, y)]
    }

    function set(mut this, x: i64, y: i64, anon val: Thing) {
        this.grid[this.idx(x, y)] = val
    }

    function draw_path(mut this, p: Path) {
        for i in 1..p.points.size() {
            this.draw_line(p1: p.points[i-1], p2: p.points[i])
        }
    }

    function draw_line(mut this, p1: Point, p2: Point) {
        if p1.x == p2.x {
            let x = p1.x
            mut low_y = p1.y
            mut high_y = p2.y
            if high_y < low_y {
                low_y = p2.y
                high_y = p1.y
            }
            for y in low_y..(high_y+1) {
                this.set(x, y, Thing::PATH())
            }
        } else if p1.y == p2.y {
            let y = p1.y
            mut low_x = p1.x
            mut high_x = p2.x
            if high_x < low_x {
                low_x = p2.x
                high_x = p1.x
            }
            for x in low_x..(high_x+1) {
                this.set(x, y, Thing::PATH())
            }
        } else {
            eprintln("diagonal lines not supported")
            abort()
        }
    }

    function print(this, bold: Point? = None) throws {
        for y in this.box.origin.y..this.box.corner.y {
            for x in this.box.origin.x..this.box.corner.x {
                mut fmt = "{:c}"
                if bold.has_value() and x == bold!.x and y == bold!.y {
                    fmt = format("\e[1m{}\e[m", fmt)
                }
                print(fmt, this.get(x, y).to_byte())
            }
            println()
        }
    }

    function simulate(mut this, debug: bool = false) throws -> Point? {
        mut (x, y) = (this.sand.x, this.sand.y)
        mut moved = true
        while moved and y < this.box.corner.y as! i64 - 1 {
            moved = false
            if this.get(x, y: y+1) is EMPTY() {
                y++
                moved = true
            } else if this.get(x: x-1, y: y+1) is EMPTY() {
                x--
                y++
                moved = true
            } else if this.get(x: x+1, y: y+1) is EMPTY() {
                x++
                y++
                moved = true
            }
            if moved and debug {
                this.set(x, y, Thing::SAND())
                this.print(bold: Point(x, y))
                this.set(x, y, Thing::EMPTY())
            }
        }
        this.set(x, y, Thing::SAND())
        if y == this.box.corner.y - 1 {
            return None
        }
        return Point(x, y)
    }
}

import jakt::libc::io { system }

function main(args: [String]) {
    mut map = SandMap::parse(read_lines(filename: "input"))
    mut count = 0
    mut bold = map.simulate()
    while bold.has_value() {
        count ++
        //print("\e[2J") // clear screen
        //map.print(bold)
        //system("sleep 0.1s".c_string())
        bold = map.simulate()
    }
    println("Part 1: {}", count)
}
